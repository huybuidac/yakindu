grammar org.eclipselabs.mscript.language.Mscript
hidden(WS, ML_COMMENT, SL_COMMENT)

generate ast "http://www.eclipselabs.org/mscript/AST/1.0.0"

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

Mscript :
	(packages+=PackageDefinition)*;
	
PackageDefinition :
	'package' name=QualifiedName '{'
		(elements+=PackageDefinitionElement)*
	'}';

PackageDefinitionElement :
	PackageDefinition | EnumerationDefinition | FunctionDefinition | VariableDeclaration;

/*
 * Enumeration
 */
 	
EnumerationDefinition :
	'enumeration' name=ID '{'
		(literals+=EnumerationLiteralDeclaration (',' literals+=EnumerationLiteralDeclaration)*)?
	'}';
	
EnumerationLiteralDeclaration :
	name=ID;

/*
 * Function
 */
 
FunctionDefinition :
	(returnType=DataTypeSpecifier)? name=ID '(' (parameters+=ParameterDeclaration (',' parameters+=ParameterDeclaration)*)? ')' body=Block;

ParameterDeclaration :
	type=DataTypeSpecifier name=ID;
	
/*
 * Statement
 */

Statement :
	Block | FeatureCallStatement | IfStatement | WhileStatement | VariableDeclaration;
	
Block :
	{Block} '{' (statements+=Statement)* '}';

FeatureCallStatement :
	featureCall=FeatureCall (assignment?='=' expression=Expression)? ';';

IfStatement :
	'if' cases+=IfCase ('elseif' cases+=IfCase)* ('else' elseBody=Block)?;

IfCase :
	condition=Expression body=Block;
	
WhileStatement :
	'while' predicate=Expression body=Block;

VariableDeclaration :
	type=DataTypeSpecifier items+=VariableDeclarationItem (',' items+=VariableDeclarationItem)* ';';
		
VariableDeclarationItem :
	name=ID ('=' initialValue=Expression)?;

/*
 * Data type specifier
 */

DataTypeSpecifier :
	PrimitiveTypeSpecifier | NamedTypeSpecifier;
	
PrimitiveTypeSpecifier :
	NumericalTypeSpecifier | BooleanTypeSpecifier | StringTypeSpecifier;
	
NumericalTypeSpecifier :
	RealTypeSpecifier | IntegerTypeSpecifier | ComplexTypeSpecifier | GaussianTypeSpecifier;
	
RealTypeSpecifier :
	{RealTypeSpecifier} 'real' ('(' unit=UnitExpression ')')? /* ('[' dimensions ']')? */;
	
IntegerTypeSpecifier :
	{IntegerTypeSpecifier} 'integer' ('(' unit=UnitExpression ')')? /* ('[' dimensions ']')? */;
	
ComplexTypeSpecifier :
	{ComplexTypeSpecifier} 'complex' ('(' unit=UnitExpression ')')? /* ('[' dimensions ']')? */;

GaussianTypeSpecifier :
	{GaussianTypeSpecifier} 'gaussian' ('(' unit=UnitExpression ')')? /* ('[' dimensions ']')? */;

BooleanTypeSpecifier :
	{BooleanTypeSpecifier} 'boolean' /* ('[' dimensions ']')? */;
	
StringTypeSpecifier :
	{StringTypeSpecifier} 'string' /* ('[' dimensions ']')? */;

NamedTypeSpecifier :
	name=QualifiedName /* ('[' dimensions ']')? */;
	
/*
 * Expressions
 */
	
Expression :
	ConditionalExpression | RangeExpression;

ConditionalExpression :
	'if' cases+=ConditionalExpressionCase ('elseif' cases+=ConditionalExpressionCase)* 'else' elseExpression=Expression;
	
ConditionalExpressionCase:
	conditionExpression=Expression 'then' thenExpression=Expression;

RangeExpression returns Expression :
	LogicalOrExpression ({RangeExpression.expressions+=current} ':' expressions+=LogicalOrExpression (':' expressions+=LogicalOrExpression)?)?;

LogicalOrExpression returns Expression :
	LogicalAndExpression ({LogicalOrExpression.operands+=current} ('or' operands+=LogicalAndExpression)+)?;
	
LogicalAndExpression returns Expression :
	LogicalNotExpression ({LogicalAndExpression.operands+=current} ('and' operands+=LogicalNotExpression)+)?;

LogicalNotExpression returns Expression :
	RelationalExpression | {LogicalNotExpression} 'not' operand=RelationalExpression;

RelationalExpression returns Expression :
	AddSubtractExpression ({RelationalExpression.operands+=current} (operators+=RelationalOperator operands+=AddSubtractExpression)+)?;

enum RelationalOperator :
	LessThan='<' |
	LessThanOrEqualTo='<=' |
	GreaterThan='>' |
	GreaterThanOrEqualTo='>=' |
	EqualTo='==' |
	NotEqualTo='<>';

AddSubtractExpression returns Expression :
	MultiplyDivideExpression ({AddSubtractExpression.operands+=current} (operators+=AddSubtractOperator operands+=MultiplyDivideExpression)+)?;

enum AddSubtractOperator :
	Add='+' |
	Subtract='-';

MultiplyDivideExpression returns Expression :
	PowerExpression ({MultiplyDivideExpression.operands+=current} (operators+=MultiplyDivideOperator operands+=PowerExpression)+)?;

enum MultiplyDivideOperator :
	Multiply='*' |
	Divide='/' |
	ElementWiseMultiply='.*' |
	ElementWiseDivide='./';
	
PowerExpression returns Expression :
	UnaryMinusExpression ({PowerExpression.operands+=current} operator=PowerOperator exponent=UnaryMinusExpression)?;

enum PowerOperator :
	Power='^' |
	ElementWisePower='.^';
	
UnaryMinusExpression returns Expression :
	PrimaryExpression | {UnaryMinusExpression} '-' operand=PrimaryExpression;

PrimaryExpression returns Expression :
	Literal |
	FeatureCall |
	ParenthesizedExpression |
	MatrixConstructionOperator |
//	ArrayConstructionOperator |
	BeginExpression |
	EndExpression;

Literal :
	NumericalLiteral | BooleanLiteral | StringLiteral;
	
NumericalLiteral :
	RealLiteral | IntegerLiteral;
	
RealLiteral :
	value=REAL (complex?='j')? ('(' unit=UnitExpression ')')?;
	
IntegerLiteral :
	value=INTEGER (complex?='j')? ('(' unit=UnitExpression ')')?;

BooleanLiteral :
	value=BooleanKind;

enum BooleanKind :
	False='false' | True='true';
	
StringLiteral :
	value=STRING;
	
QualifiedName :
	identifiers+=ID ('::' identifiers+=ID)*;

//	arguments+=Expression ((',' arguments+=Expression)* (',' namedArguments+=NamedArgument)* | 'for' forIndexList=ForIndexList) |
//	namedArguments+=NamedArgument (',' namedArguments+=NamedArgument)*;

//NamedArgument :
//	identifier=ID '=' expression=Expression;

FeatureCall :
	(global?='::')? name=QualifiedName (componentReferences+=ComponentReference)* (operationCall?='(' (arguments+=Expression (',' arguments+=Expression)*)? ')')?;
	
ComponentReference :
	ArrayReference |  MemberReference;

ArrayReference :
	'[' subscripts+=Subscript (',' subscripts+=Subscript)* ']';
	
MemberReference :
	'.' name=ID;
	
Subscript :
	ColonSubscript | ExpressionSubscript;

ColonSubscript :
	{ColonSubscript} ':';

ExpressionSubscript :
	expression=Expression;
	
MatrixConstructionOperator :
	'[' expressionLists+=ExpressionList (';' expressionLists+=ExpressionList)* ']';
	
ExpressionList :
	expressions+=Expression (',' expressions+=Expression)*;

ParenthesizedExpression :
//	'(' expressionList=OutputExpressionList ')';
	'(' expression=Expression ')';

//OutputExpressionList :
//	expressions+=FirstOutputExpression (expressions+=RightOutputExpression)*;
//
//FirstOutputExpression returns Expression :
//	{EmptyExpression} | Expression;
//
//RightOutputExpression returns Expression :
//	',' {EmptyExpression} | ',' Expression;

BeginExpression :
	{BeginExpression} 'begin';

EndExpression :
	{EndExpression} 'end';
	
//ForIndexList :
//	indices+=ForIndex (',' indices+=ForIndex)*;
//	
//ForIndex :
//	identifier=ID ('in' inExpression=Expression)?;

/*
 * Unit expressions
 */
	
UnitExpression :
	numerator=UnitExpressionNumerator ('/' denominator=UnitExpressionDenominator)?;

UnitExpressionNumerator :
	one=INTEGER /* must be '1' */ | factors+=UnitExpressionFactor ('*' factors+=UnitExpressionFactor)*;

UnitExpressionDenominator :
	factors+=UnitExpressionFactor | '(' factors+=UnitExpressionFactor ('*' factors+=UnitExpressionFactor)* ')';

UnitExpressionFactor :
	operand=ID ('^' exponent=UnitExpressionExponent)?;

UnitExpressionExponent :
	(negative?='-')? value=INTEGER;

/*
 * Terminals
 */
 
terminal ID :
	('_' | 'a'..'z' | 'A'..'Z') ('0'..'9' | '_' | 'a'..'z' | 'A'..'Z')*;

terminal STRING : 
	'"' (!('\\' | '"') | '\\' ("'" | '"' | '?' | '\\' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v'))* '"';

terminal REAL returns ecore::EDouble :
	('0'..'9')+ '.' ('0'..'9')* (('e' | 'E') ('+'|'-')? ('0'..'9')+)?;

terminal INTEGER returns ecore::ELong :
	('0'..'9')+;

terminal ML_COMMENT :
	'/*' -> '*/';

terminal SL_COMMENT :
	'//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS :
	(' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER :
	.;
